NUM – fixed point integer values with a sign, accuracy of the type signed long int (32 bits)
STRING – alpha-numeric string embraced with double quotation sign (”)
IDENT – variable identifier (string, standard naming convention)
Other key words: and, or, not, if, then, else, for, to, do, break, continue, print,
readint, readstr, begin, end, exit, substring, length, position,
concatenate.


The language grammar:
*** numerical expression, which value is a number
num_expr = num_expr "+" t_num_expr
    | num_expr "-" t_num_expr
    | t_num_expr ;

t_num_expr = t_num_expr "*" f_num_expr
    | t_num_expr "/" f_num_expr
    | t_num_expr "%" f_num_expr
    | f_num_expr ;

f_num_expr = NUM | IDENT
    | "readint"
    | "-" num_expr
    | "(" num_expr ")"
    | "length" "(" str_expr ")"
    | "position" "(" str_expr "," str_expr ")" ;

*** expression, which value is a string
str_expr = STRING | IDENT
    | "readstr"
    | "concatenate(" str_expr "," str_expr ")"
    | "substring(" str_expr "," num_expr "," num_expr ")" ;

*** logical relations
num_rel = "=" | "<" | "<=" | ">" | ">=" | "<>" ;

str_rel = "==" | "!=" ;

bool_expr = bool_expr "or" t_bool_expr
    | t_bool_expr ;

t_bool_expr = t_bool_expr "and" f_bool_expr
    | f_bool_expr ;

f_bool_exp = "true" | "false"
    | "(" bool_expr ")"
    | "not" bool_expr
    | num_expr num_rel num_expr
    | str_expr str_rel str_expr ;

*** basic constucts
simple_instr = assign_stat
    | if_stat
    | for_stat
    | "begin" instr "end"
    | output_stat
    | "break" ;
    | "continue"
    | "exit" ;
    
*** instuction sequence

instr = instr simple_instr ";" | epsilon ;

*** assignment

assign_stat = IDENT ":=" num_expr
    | IDENT ":=" str_expr ;
    
*** conditional statement

if_stat = "if" bool_expr "then" simple_instr
    | "if" bool_expr "then" simple_instr "else" simple_instr ;
*** "for" loop

for_stat = "for" IDENT ":=" num_expr "to" num_expr "do"
    simple_instr ;
    
*** printing to the screen

output_stat = "print(" num_expr ")"
    | "print(" str_expr ")" ;

*** progam itself

program = instr ;